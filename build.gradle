/*
*   Gradle file to build the Unity plugin for the Adivery SDK.
*   Useage: ./gradlew exportPackage
*/

defaultTasks 'exportPackage'

// Project level variables.
project.ext {
    sdk_root = System.getProperty("ANDROID_HOME")
    if (sdk_root == null || sdk_root.isEmpty()) {
        sdk_root = System.getenv("ANDROID_HOME")
    }
    unity_exe = System.getProperty("UNITY_EXE")
    if (unity_exe == null || unity_exe.isEmpty()) {
        unity_exe = System.getenv("UNITY_EXE")
    }

    pluginSource = file('source/plugin').absolutePath
    exportPath = file('Adivery.unitypackage').absolutePath
}

// Delete existing android plugin aar file.
task clearAar(type: Delete) {
    delete 'source/android-library/app/build/libs/adivery-unity.aar'
}

// Build jar from android plugin source files using existing Gradle build file.
task buildAndroidPluginAar(type: GradleBuild) {
    buildFile = 'source/android-library/app/build.gradle'
    tasks = ['makeAar']
}

// Move android plugin jar to temporary build directory.
task copyAndroidLibraryAar(type: Copy) {
    from("source/android-library/app/build/libs")
    into("${pluginSource}/Assets/Plugins/Android")
    include('adivery-unity.aar')
}

copyAndroidLibraryAar.dependsOn(clearAar, buildAndroidPluginAar)

// Build unity package using through command line interface.
// Create new unity project with files in temporary build directory and export files within Assets/Adivery
// to a unity package.
// Command line usage and arguments documented at http://docs.unity3d.com/Manual/CommandLineArguments.html.
task exportPackage() {
    description = "Creates and exports the Plugin unity package"
    doLast {
        exec {
            executable "${unity_exe}"
            args "-batchmode",
                 "-projectPath", "${pluginSource}",
                 "-logFile", "unity.log",
                 "-exportPackage",
                 "Assets/Adivery",
                 "Assets/Plugins/Android/adivery-sdk.aar",
                 "Assets/Plugins/Android/adivery-admob-adapter.aar",
                 "Assets/Plugins/Android/adivery-unity.aar",
                 "${exportPath}",
                 "-quit"
        }
    }
}

exportPackage.dependsOn(copyAndroidLibraryAar)